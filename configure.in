dnl Process this file with autoconf to produce a configure script.

AC_INIT(love, 0.0.1, http://love.github.com/, love)
AC_PREREQ(2.50)

LOVE_MAJOR_VERSION=0
LOVE_MINOR_VERSION=0
LOVE_MICRO_VERSION=1
LOVE_VERSION=$LOVE_MAJOR_VERSION.$LOVE_MINOR_VERSION.$LOVE_MICRO_VERSION

AC_SUBST(LOVE_MAJOR_VERSION)
AC_SUBST(LOVE_MINOR_VERSION)
AC_SUBST(LOVE_MICRO_VERSION)
AC_SUBST(LOVE_VERSION)

AM_INIT_AUTOMAKE(love, $LOVE_VERSION)

AC_PROG_CC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_YACC
AM_PROG_LEX

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h])
AC_CHECK_HEADERS([malloc.h])
AC_CHECK_HEADERS([sys/types.h])
AC_CHECK_HEADERS([dirent.h])
AC_CHECK_HEADERS([stdlib.h])
AC_CHECK_HEADERS([strings.h])
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([locale.h])
AC_CHECK_HEADERS([langinfo.h])

AC_CHECK_FUNCS(bzero memset, break)

AC_C_CONST
AC_C_BIGENDIAN

AC_TYPE_SIZE_T

dnl LOVE_LIBTOOL_VERSION=0:0:0
dnl AC_SUBST(LOVE_LIBTOOL_VERSION)

dnl == LLVM CHECK ========================================================

AC_ARG_WITH(llvm-config, [  --with-llvm-config=PATH   Location of LLVM llvm-config []], llvm_config="$withval", llvm_config="")

if test "X$llvm_config" != "X" ; then
  AC_MSG_CHECKING(for $llvm_config)

  if test -f $llvm_config ; then
    LLVM_CONFIG=$llvm_config
    AC_MSG_RESULT(yes)
  else
    AC_MSG_RESULT(no - searching PATH)
  fi
fi
if test "X$LLVM_CONFIG" = "X"; then
  AC_CHECK_PROGS(LLVM_CONFIG, llvm-config)
fi

if test -n "$LLVM_CONFIG"; then
  vers=`$LLVM_CONFIG --version 2>/dev/null | sed -e 's/llvm //' | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
  if test -n "$vers" && test "$vers" -ge 2004000
  then
     LLVM_LIBS="`$LLVM_CONFIG --libs`"
     LLVM_RPATH=
     for args in $LLVM_LIBS; do
          case $args in
            -L*)
               LLVM_RPATH="$LLVM_RPATH $args"
               ;;
          esac
     done
     LLVM_RPATH=`echo $LLVM_RPATH | sed -e "s/-L/-R/g"`
     LLVM_CFLAGS="`$LLVM_CONFIG --cflags`"
  else
     AC_MSG_WARN([You need at least LLVM 2.4])
     HELP_SUBDIR=
  fi
fi

AC_SUBST(LLVM_LIBS)
AC_SUBST(LLVM_CFLAGS)

dnl =========================================================================

AM_CONFIG_HEADER(src/config.h)

AC_OUTPUT(Makefile src/Makefile)

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:       ${srcdir}
  Compiler:                   ${CC}
  Compiler flags:             ${CFLAGS}
  Host System Type:           ${host}
  Install path:               ${prefix}

------------------------------------------------------------------------"
